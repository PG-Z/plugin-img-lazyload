import org.yaml.snakeyaml.DumperOptions
import org.yaml.snakeyaml.Yaml

plugins {
    id 'java'
    id "com.github.node-gradle.node" version "7.0.2"
    id "io.freefair.lombok" version "8.0.1"
    id "run.halo.plugin.devtools" version "0.1.1"
}

group 'run.halo.starter'
sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation platform('run.halo.tools.platform:plugin:2.13.0-SNAPSHOT')
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

node {
    nodeProjectDir = file("${project.projectDir}/ui")
}

tasks.register('buildFrontend', PnpmTask) {
    args = ['build']
    dependsOn('installDepsForUI')
}

tasks.register('installDepsForUI', PnpmTask) {
    args = ['install']
}

build {
    // build frontend before build
    tasks.named('compileJava').configure {
        dependsOn('buildFrontend')
    }
}

halo {
    version = '2.17'
}

tasks.register('readYamlVersion') {
    doLast {
        def yamlFile = file('plugin.yaml')
        def yaml = new Yaml().load(yamlFile.newInputStream())
        def version = yaml.spec.version
        println version
    }
}

// 更新版本号
tasks.register('updateVersion') {
    doLast {
        def yamlFile = file('src/main/resources/plugin.yaml')
        def yaml = new Yaml().load(yamlFile.newInputStream())
        yaml.spec.version = project.version
        def newYaml = new DumperOptions()
        newYaml.setDefaultFlowStyle(DumperOptions.FlowStyle.BLOCK)
        def yamlOutput = new Yaml(newYaml).dump(yaml)
        yamlFile.write(yamlOutput)
    }
}
// 构建之前先执行更新版本号的任务
build.dependsOn updateVersion

